buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 18
    buildToolsVersion '19.1.0'

    defaultConfig {
        applicationId "nz.ac.auckland.lablet"
        minSdkVersion 18
        targetSdkVersion 18
        versionCode 303
        versionName "3.0.1"
        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('keystore.properties').newDataInputStream())

        release {
            storeFile file(properties.getProperty('localStoreFile'))
            storePassword properties.getProperty('localStorePassword')
            keyAlias properties.getProperty('localKeyAlias')
            keyPassword properties.getProperty('localKeyPassword')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

configurations {
    doclet
}

dependencies {
    doclet group: 'org.jboss.apiviz', name: 'apiviz', version: '1.3.2.GA'
    compile project(':openCVLibrary2411')
}

android.applicationVariants.all { variant ->
    task("generate${variant.name}Javadoc", type: Javadoc) {
        source = variant.javaCompile.source
        def androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"

        classpath = files(variant.javaCompile.classpath.files, androidJar)
        options {
            links "http://docs.oracle.com/javase/7/docs/api/"
            linksOffline "http://d.android.com/reference","${android.sdkDirectory}/docs/reference"
            doclet 'org.jboss.apiviz.APIviz'
            docletpath file(configurations.doclet.asPath)
        }
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile "au.com.bytecode:opencsv:2.4"
    compile "edu.emory.mathcs:JTransforms:2.4"
    compile "org.luaj:luaj-jse:3.0-beta2"
    compile "com.android.support:support-annotations:20.0.0"
    compile "com.android.support:support-v4:21.0.2"
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.github.wseemann:FFmpegMediaMetadataRetriever:1.0.1"
}
